(* Distributed under the terms of the MIT license. *)
From Stdlib Require Import ssreflect Morphisms Orders Setoid.
From MetaRocq.Utils Require Import utils.
From MetaRocq.Common Require Export Kernames.
From Stdlib Require Floats.SpecFloat.
From Equations Require Import Equations.

(** Identifiers that are allowed to be anonymous (i.e. "_" in concrete syntax). *)
Inductive name : Set :=
| nAnon
| nNamed (_ : ident).
Derive NoConfusion EqDec for name.

Module QGlobal.

  Record t := {
    library : dirpath ;
    id : ident
  }.

  Notation make := Build_t.

  Definition repr q := (q.(library), q.(id)).

  Definition equal q1 q2 :=
    repr q1 == repr q2.

  Definition compare q1 q2 :=
    let c := compare_ident q1.(id) q2.(id) in
    match c with
    | Lt | Gt => c
    | Eq => DirPathOT.compare q1.(library) q2.(library)
    end.

  Definition to_string q :=
    string_of_dirpath q.(library) ^ "." ^ q.(id).

  #[global, refine]
  Instance reflect_qglobal : ReflectEq QGlobal.t := {
    eqb := equal
  }.
  Proof. 
    intros [lib1 id1] [lib2 id2]; unfold equal, repr; cbn.
    case: eqb_spec ; nodec.
    now constructor.
  Qed.

  Lemma eqb_refl (g : t) : eqb g g = true.
  Proof.
    destruct g; cbn; apply eqb_refl.
  Qed.

  #[global]
  Instance eq_dec_qglobal : EqDec t.
  Proof. 
    intros q1 q2.
    destruct (q1 == q2) eqn:e.
    - apply eqb_eq in e. now left.
    - right. intros f. rewrite f in e. subst.
      rewrite eqb_refl in e. discriminate.
  Defined.

  Inductive lt : t -> t -> Prop :=
  | ltIdQGlobal lib1 id1 lib2 id2 : 
    IdentOT.lt id1 id2 ->
    lt (make lib1 id1) (make lib2 id2)
  | ltLibQGlobal id lib1 lib2 : 
    DirPathOT.lt lib1 lib2 ->
    lt (make lib1 id) (make lib2 id).

  Derive Signature for lt.

  #[global] Instance lt_strorder : StrictOrder lt.
  Proof.
    constructor.
    - intros [lib id] X. inversion X; subst;
      now eapply irreflexivity in H0.
    - intros q1 q2 q3 H1 H2; inversion H1; inversion H2; subst; injection H5; intros; subst;
      try now constructor.
      * constructor; now etransitivity.
      * constructor; now etransitivity.
  Qed.
  
  Definition ltb (q1 q2 : QGlobal.t) : bool :=
    match QGlobal.compare q1 q2 with
    | Lt => true
    | _ => false
    end.

  Definition compare_spec : forall x y, CompareSpec (eq x y) (lt x y) (lt y x) (compare x y).
  Proof.
    intros [lib1 id1] [lib2 id2]. rewrite /compare. cbn. rewrite /compare_ident.
    destruct (IdentOT.compare_spec id1 id2) as [e | lt | gt]; subst.
    - destruct (DirPathOT.compare_spec lib1 lib2) as [e' | lt' | gt']; subst;
      now repeat constructor.
    - constructor. now constructor.
    - constructor. now constructor.
  Qed.

  Lemma reflect_lt_qglobal (x y : t) : reflectProp (lt x y) (ltb x y).
  Proof.
    destruct (ltb x y) eqn:e; constructor; destruct x as [lib1 id1], y as [lib2 id2].
    - rewrite /ltb in e.
      destruct (compare_spec (make lib1 id1) (make lib2 id2)); try discriminate. 
      assumption.
    - intro f. inversion f; subst.
      * rewrite /ltb /compare in e. rewrite /compare_ident in e. unfold IdentOT.lt in H0. rewrite H0 in e. discriminate.
      * rewrite /ltb /compare in e. rewrite /compare_ident in e. rewrite IdentOT.compare_refl in e. cbn in e. move/DirPathOT.compare_lt_lt: H0 => H0; rewrite H0 in e. discriminate. 
  Qed.

  Inductive le : t -> t -> Prop :=
  | leeqQGlobal q : le q q
  | leltQGlobal q1 q2 : lt q1 q2 -> le q1 q2.
    
  Derive Signature for le.
  
  #[global] Instance le_trans : Transitive le.
  Proof.
    intros [lib1 id1] [lib2 id2] [lib3 id3] H1 H2.
    inversion H1; inversion H2; subst; try now constructor.
    constructor. etransitivity; tea.
  Qed.

  #[global] Instance le_preorder : PreOrder le.
  Proof.
    constructor.
    - intros [lib id]. constructor.
    - intros v1 v2 v3 X1 X2; inversion X1; inversion X2; subst; try now constructor.
      etransitivity; tea.
  Qed.  

  Definition leqb (q1 q2 : t) : bool := 
    (* (q1 == q2) || ltb q1 q2. *)
    match QGlobal.compare q1 q2 with
    | Gt => false
    | _ => true
    end.

  Lemma reflect_le_qglobal (x y : t) : reflectProp (le x y) (leqb x y).
  Proof.
    destruct (leqb x y) eqn:e; constructor; destruct x as [lib1 id1], y as [lib2 id2].
    - rewrite /leqb in e. destruct (compare_spec (make lib1 id1) (make lib2 id2)); try discriminate.
      * injection H; intros; subst. now constructor.
      * now constructor.
    - intro f. inversion f; subst.
      * rewrite /leqb in e. destruct (compare_spec (make lib2 id2) (make lib2 id2)); try discriminate. now apply irreflexivity in H.
      * rewrite /leqb in e. destruct (compare_spec (make lib1 id1) (make lib2 id2)); try discriminate. eapply transitivity in H0. now apply irreflexivity in H0. assumption.
  Qed.

  Definition leq_qglobal_dec v v' : {le v v'} + {~le v v'}.
  Proof.
    destruct v as [lib1 id1], v' as [lib2 id2].
    destruct (leqb (make lib1 id1) (make lib2 id2)) eqn:e.
    - now move/reflect_le_qglobal: e.
    - right; move/reflect_le_qglobal=> f; congruence.
  Defined.

End QGlobal.

Module QVar.
  Inductive repr_ {V : Set} := Var (_ : V) | Global (_ : QGlobal.t).
  Derive NoConfusion for repr_.
  Arguments repr_ : clear implicits.

  Definition repr := repr_ nat.
  Definition t := repr.

  Definition var (n : nat) : t := Var n.

  Definition eqb {V : Set} `{ReflectEq V} (v1 v2 : repr_ V) : bool :=
    match v1, v2 with
    | Var i, Var j => eqb i j
    | Global q1, Global q2 => QGlobal.equal q1 q2
    | _, _ => false
    end.

  Lemma eqb_refl {V : Set} `{ReflectEq V} (v : repr_ V) : eqb v v = true.
  Proof.
    destruct v; cbn; apply eqb_refl.
  Qed.

  #[global, program] Instance reflect_eq_qvar {V : Set} `{ReflectEq V} : ReflectEq (repr_ V) :=
    { eqb := eqb }.
  Next Obligation.
    destruct x as [v1 | t1].
    - destruct y as [v2 | t2]; cbn; try case eqb_spec; nodec. now constructor.
    - destruct y as [v2 | t2]; cbn; try case eqb_spec; nodec.
      destruct t1 as [lib1 id1], t2 as [lib2 id2]. 
      unfold QGlobal.equal, QGlobal.repr; cbn.
      case: eqb_spec; nodec.
      now constructor.
  Qed.

  #[global] Instance eq_dec_qvar {V : Set} `{EqDec V} : EqDec (repr_ V).
  Proof.
    intros [v1 | t1] [v2 | t2]; cbn; try decide equality;
    (* Why is this instance not picked up by automation? *)
    eapply QGlobal.eq_dec_qglobal.
  Qed.

  Inductive lt_ {V V_lt} : repr_ V -> repr_ V -> Prop :=
  | ltVarVar i j : V_lt i j -> lt_ (Var i) (Var j)
  | ltGlobalGlobal t1 t2 : QGlobal.lt t1 t2 -> lt_ (Global t1) (Global t2)
  | ltVarGlobal i q1 : lt_ (Var i) (Global q1).

  Derive Signature for lt_.
  Arguments lt_ {V} V_lt.

  Definition lt := lt_ Nat.lt.
  
  #[global] Instance lt_strorder : StrictOrder lt.
  Proof.
    constructor.
    - intros v X; inversion X;
      now eapply irreflexivity in H1.
    - intros v1 v2 v3 X1 X2.
      inversion X1; inversion X2; subst; try congruence; try now constructor.
      * inversion H3; subst. constructor. now etransitivity; tea. 
      * inversion H3; subst. constructor. now etransitivity; tea.
  Qed.

  Definition lt_compat : Proper (eq ==> eq ==> iff) lt.
  Proof.
    intros x y e z t e'. hnf in * |- ; subst. reflexivity.
  Qed.

  Definition ltb_ {V V_ltb} (v1 v2 : repr_ V) : bool :=
    match v1, v2 with
    | Var i, Var j => V_ltb i j
    | Global t1, Global t2 => QGlobal.ltb t1 t2
    | Var _, _ => true
    | _, _ => false
    end.
    
  Arguments ltb_ {V} V_ltb.

  Definition ltb := ltb_ Nat.ltb.

  Lemma reflect_lt_qvar {V : Set} (x y : t) : reflectProp (lt x y) (ltb x y).
  Proof.
    destruct (ltb x y) eqn:e; constructor; destruct x as [n1 | [lib1 id1]], y as [n2 | [lib2 id2]]; rewrite /ltb /ltb_ in e; try congruence; try now (intro f; inversion f).
    - constructor. now apply Nat.ltb_lt.
    - now constructor.
    - constructor. apply (QGlobal.reflect_lt_qglobal _ _ e).
    - intro f; inversion f; subst.
      move/QGlobal.reflect_lt_qglobal: H1. congruence.
  Qed.

  Inductive le_ {V V_le} : repr_ V -> repr_ V -> Prop :=
  | leVarVar i j : V_le i j -> le_ (Var i) (Var j)
  | leGlobalGlobal t1 t2 : QGlobal.le t1 t2 -> le_ (Global t1) (Global t2)
  | leVarGlobal i q1 : le_ (Var i) (Global q1).
  Derive Signature for le_.
  Arguments le_ {V} V_le.

  Definition le := le_ Nat.le.
  
  #[global] Instance le_preorder : PreOrder le.
  Proof.
    constructor.
    - intros i. destruct i; constructor; constructor.
    - intros v1 v2 v3 X1 X2; inversion X1; inversion X2; subst; try discriminate; constructor;
      now etransitivity; tea.
  Qed. 

  Definition le_compat : Proper (eq ==> eq ==> iff) le.
  Proof.
    intros x y e z t e'. subst; reflexivity.
  Qed.

  Definition leqb_ {V V_leqb} (v1 v2 : repr_ V) : bool :=
    match v1, v2 with
    | Var i, Var j => V_leqb i j
    | Global t1, Global t2 => QGlobal.leqb t1 t2
    | Var _, _ => true
    | _, _ => false
    end.
  Arguments leqb_ {V} V_leqb.

  Definition leqb := leqb_ Nat.leb.

  Lemma reflect_le_qvar {V : Set} (x y : t) : reflectProp (le x y) (leqb x y).
  Proof.
    destruct (leqb x y) eqn:e; constructor; destruct x, y; rewrite /leqb /leqb_ in e; try discriminate; try now (intro f; inversion f).
    - constructor. now apply Nat.leb_le.
    - now constructor.
    - constructor. apply (QGlobal.reflect_le_qglobal _ _ e). 
    - intro f. inversion f; subst. move/QGlobal.reflect_le_qglobal: H1. congruence.
  Qed.

  Definition compare (v1 v2 : t) : comparison
    := match v1, v2 with
       | Var i, Var j => Nat.compare i j
       | Global t1, Global t2 => QGlobal.compare t1 t2 
       | Var _, _ => Lt 
       | _, Var _ => Gt
       end.

  Lemma compare_spec x y : CompareSpec (eq x y) (lt x y) (lt y x) (compare x y).
  Proof.
    cbv [compare].
    destruct x as [i | q1], y as [j | q2].
    all: lazymatch goal with
         | [ |- context[Nat.compare ?x ?y] ]
           => destruct (Nat.compare_spec x y)
         | _ => idtac
         end.
    all: repeat constructor; try apply f_equal; try assumption.
    destruct (QGlobal.compare_spec q1 q2); now (repeat constructor).
  Qed.

  Definition eq_ {V} eq_V (q1 q2 : repr_ V) : Prop :=
    match q1, q2 with
    | Var q1, Var q2 => eq_V q1 q2
    | Global t1, Global t2 => t1 = t2
    | _, _ => False
    end.

  Definition eq : t -> t -> Prop := eq_ (fun n m => n = m).

  #[global] Instance eq_equiv : Equivalence eq.
  Proof.
    split; unfold eq.
    - intros []; now cbn.
    - intros [] []; now cbn.
    - intros [] [] []; now cbn.
  Qed.

  Definition eq_dec_ {V : Set} {eq_V} (eq_dec_V : forall v1 v2 : V, {eq_V v1 v2} + {~ (eq_V v1 v2)})
    (v1 v2 : repr_ V) : {eq_ eq_V v1 v2} + {~ (eq_ eq_V v1 v2)}.
  Proof. destruct v1, v2; unfold eq_; try tauto.
    - apply eq_dec_V.
    - apply QGlobal.eq_dec_qglobal.
  Defined. 

  Definition eq_dec := eq_dec_ Nat.eq_dec.

  (* Definition sup_ {V} V_sup (x y : repr_ V) : repr_ V :=
    match x, y with
    | Var n, Var m => Var (V_sup n m)
    end. *)

  (* Definition sup : t -> t -> t := sup_ Nat.max. *)

  (* Lemma sup_comm_ {V : Set} V_sup :
    (forall n m : V, V_sup n m = V_sup m n) ->
    forall (x y : repr_ V), sup_ V_sup x y = sup_ V_sup y x.
  Proof.
    intros Vcomm ??. destruct x, y; cbn.
    f_equal. apply Vcomm.
  Qed. *)

  (* Lemma sup_comm (x y : t) : sup x y = sup y x.
  Proof.
    apply sup_comm_, Nat.max_comm.
  Qed. *)

  (* Lemma sup_idem (x : t) : sup x x = x.
  Proof.
    destruct x; cbn. now rewrite Nat.max_id.
  Qed. *)
End QVar.

Definition leq_qvar_dec v v' : {QVar.le v v'} + {~QVar.le v v'}.
Proof.
  destruct v as [i | q1], v' as [j | q2]; cbv[QVar.le].
  - destruct (le_dec i j).
    * left. now constructor.
    * right. intro. inversion H. now apply n.
  - left. now constructor.
  - right. intro. inversion H.
  - destruct (QGlobal.leq_qglobal_dec q1 q2) as [Hle | Hnle].
    * now left; constructor.
    * right. intro f. apply Hnle. now inversion f. 
Qed.

Definition string_of_qvar (q : QVar.t) :=
  match q with
  | QVar.Var i => "α" ^ string_of_nat i
  | QVar.Global q => "γ" ^ QGlobal.to_string q
  end.

Inductive relevance : Set := Relevant | Irrelevant | RelevanceVar (_ : QVar.t).
Derive NoConfusion EqDec for relevance.

Definition on_relevance {T} (on_qvar : QVar.t -> T) (def : T) r :=
  match r with Relevant | Irrelevant => def | RelevanceVar qv => on_qvar qv end.

(** Binders annotated with relevance *)
Record binder_annot (A : Type) := mkBindAnn { binder_name : A; binder_relevance : relevance }.

Arguments mkBindAnn {_}.
Arguments binder_name {_}.
Arguments binder_relevance {_}.

Derive NoConfusion for binder_annot.

#[global] Instance eqdec_binder_annot (A : Type) (e : Classes.EqDec A) : Classes.EqDec (binder_annot A).
Proof. ltac:(Equations.Prop.Tactics.eqdec_proof). Qed.

Definition map_binder_annot {A B} (f : A -> B) (b : binder_annot A) : binder_annot B :=
  {| binder_name := f b.(binder_name); binder_relevance := b.(binder_relevance) |}.

Definition cmp_binder_annot {A B} cmp_rel (b : binder_annot A) (b' : binder_annot B) : Prop :=
  cmp_rel b.(binder_relevance) b'.(binder_relevance).

Definition eq_binder_annot {A B} (b : binder_annot A) (b' : binder_annot B) : Prop :=
  b.(binder_relevance) = b'.(binder_relevance).

(** Type of annotated names *)
Definition aname := binder_annot name.
#[global] Instance anqme_eqdec : Classes.EqDec aname := _.

Definition eqb_binder_annot {A} (b b' : binder_annot A) : bool :=
  match Classes.eq_dec b.(binder_relevance) b'.(binder_relevance) with
  | left _ => true
  | right _ => false
  end.

Definition string_of_name (na : name) :=
  match na with
  | nAnon => "_"
  | nNamed n => n
  end.

Definition string_of_relevance (r : relevance) :=
  match r with
  | Relevant => "Relevant"
  | Irrelevant => "Irrelevant"
  | RelevanceVar qv => "RelevanceVar(" ^ string_of_qvar qv ^ ")"
  end.

(** The kind of a cast *)
Inductive cast_kind : Set :=
| VmCast
| NativeCast
| Cast.
Derive NoConfusion EqDec for cast_kind.

Record case_info := mk_case_info {
  ci_ind : inductive;
  ci_npar : nat;
  (* Not implemented yet, as the representation in PCUIC doesn't need this cached information.
     ci_cstr_nargs : list nat; (* The number of REAL arguments of each constructor (no params, no lets) *)
     ci_cstr_ndecls : list nat; (* The number of arguments of each constructor (no params but lets included) *)   *)
  ci_relevance : relevance }.
Derive NoConfusion EqDec for case_info.

Definition map_case_info f ci :=
  {| ci_ind := ci.(ci_ind); ci_npar := ci.(ci_npar); ci_relevance := f ci.(ci_relevance) |}.

Definition string_of_case_info ci :=
  "(" ^ string_of_inductive ci.(ci_ind) ^ "," ^
  string_of_nat ci.(ci_npar) ^ "," ^
  (* string_of_list string_of_nat ci.(ci_cstr_nargs) ^ "," ^
  string_of_list string_of_nat ci.(ci_cstr_ndecls) ^ "," ^ *)
  string_of_relevance ci.(ci_relevance) ^ ")".

Inductive recursivity_kind :=
  | Finite (* = inductive *)
  | CoFinite (* = coinductive *)
  | BiFinite (* = non-recursive, like in "Record" definitions *).
Derive NoConfusion EqDec for recursivity_kind.

(* The kind of a conversion problem *)
Inductive conv_pb :=
  | Conv
  | Cumul.
Derive NoConfusion EqDec for conv_pb.

Definition conv_pb_leqb (pb1 pb2 : conv_pb) : bool
  := match pb1, pb2 with
     | Cumul, Conv => false
     | _, _ => true
     end.

(* This opaque natural number is a hack to inform unquoting to generate
  a fresh evar when encountering it. *)
Definition fresh_evar_id : nat. exact 0. Qed.

(* Parametrized by term because term is not yet defined *)
Record def term := mkdef {
  dname : aname; (* the name, annotated with relevance **)
  dtype : term;
  dbody : term; (* the body (a lambda term). Note, this may mention other (mutually-defined) names **)
  rarg  : nat  (* the index of the recursive argument, 0 for cofixpoints **) }.

Arguments dname {term} _.
Arguments dtype {term} _.
Arguments dbody {term} _.
Arguments rarg {term} _.

Derive NoConfusion for def.
#[global] Instance def_eq_dec {A} : Classes.EqDec A -> Classes.EqDec (def A).
Proof. ltac:(Equations.Prop.Tactics.eqdec_proof). Qed.

Definition string_of_def {A} (f : A -> string) (def : def A) :=
  "(" ^ string_of_name (binder_name (dname def))
      ^ "," ^ string_of_relevance (binder_relevance (dname def))
      ^ "," ^ f (dtype def)
      ^ "," ^ f (dbody def)
      ^ "," ^ string_of_nat (rarg def) ^ ")".

Definition print_def {A} (f : A -> string) (g : A -> string) (def : def A) :=
  string_of_name (binder_name (dname def)) ^ " { struct " ^ string_of_nat (rarg def) ^ " }" ^
                 " : " ^ f (dtype def) ^ " := " ^ nl ^ g (dbody def).

Definition map_def_gen {A B} (anamef : aname -> aname) (tyf bodyf : A -> B) (d : def A) :=
  {| dname := anamef d.(dname);
    dtype := tyf d.(dtype);
    dbody := bodyf d.(dbody);
    rarg := d.(rarg) |}.

Notation map_def := (map_def_gen (fun x => x)).

Lemma map_dtype {A B} (f : A -> B) (g : A -> B) (d : def A) :
  f (dtype d) = dtype (map_def f g d).
Proof. destruct d; reflexivity. Qed.

Lemma map_dbody {A B} (f : A -> B) (g : A -> B) (d : def A) :
  g (dbody d) = dbody (map_def f g d).
Proof. destruct d; reflexivity. Qed.

Lemma map_dname {A B} (f : A -> B) (g : A -> B) (d : def A) :
  dname d = dname (map_def f g d).
Proof. destruct d; reflexivity. Qed.

Definition mfixpoint term := list (def term).

Definition test_def_gen {A} (namef : aname -> bool) (tyf bodyf : A -> bool) (d : def A) :=
  namef d.(dname) && tyf d.(dtype) && bodyf d.(dbody).

Definition test_def {A} (tyf bodyf : A -> bool) (d : def A) :=
  tyf d.(dtype) && bodyf d.(dbody).

Definition tFixProp_gen {A} (P : aname -> Type) (P' P'' : A -> Type) (m : mfixpoint A) :=
  All (fun x : def A => P x.(dname) * P' x.(dtype) * P'' x.(dbody))%type m.

Notation tFixProp := (tFixProp_gen (fun _ => True)).

Lemma map_def_gen_map {A B C} (f f' : aname -> aname) (g h : B -> C) (g' h' : A -> B) (d : def A) :
  map_def_gen f g h (map_def_gen f' g' h' d) = map_def_gen (f ∘ f') (g ∘ g') (h ∘ h') d.
Proof.
  destruct d; reflexivity.
Qed.

Lemma map_def_map_def {A B C} (f f' : B -> C) (g g' : A -> B) (d : def A) :
  map_def f f' (map_def g g' d) = map_def (f ∘ g) (f' ∘ g') d.
Proof.
  destruct d; reflexivity.
Qed.

Lemma compose_map_def {A B C} (f f' : B -> C) (g g' : A -> B) :
  (map_def f f') ∘ (map_def g g') = map_def (f ∘ g) (f' ∘ g').
Proof. reflexivity. Qed.

Lemma map_def_id {t} x : map_def (@id t) (@id t) x = id x.
Proof. now destruct x. Qed.
#[global] Hint Rewrite @map_def_id @map_id : map.

Lemma map_def_gen_spec {A B} (P : aname -> Type) (P' P'' : A -> Type) (f f' : aname -> aname)
  (g g' h h' : A -> B) (x : def A) :
  P'' x.(dbody) -> P' x.(dtype) -> P x.(dname) -> (forall x, P x -> f x = f' x) ->
  (forall x, P' x -> g x = g' x) -> (forall x, P'' x -> h x = h' x) ->
  map_def_gen f g h x = map_def_gen f' g' h' x.
Proof.
  intros. destruct x. unfold map_def. simpl.
  now rewrite !H // !H0 // !H1.
Qed.

Lemma map_def_spec {A B} (P P' : A -> Type) (f f' g g' : A -> B) (x : def A) :
  P' x.(dbody) -> P x.(dtype) -> (forall x, P x -> f x = g x) ->
  (forall x, P' x -> f' x = g' x) ->
  map_def f f' x = map_def g g' x.
Proof.
  intros; eapply map_def_gen_spec with (P := fun x => True); eauto.
Qed.

#[global] Hint Extern 10 (_ < _)%nat => lia : all.
#[global] Hint Extern 10 (_ <= _)%nat => lia : all.
#[global] Hint Extern 10 (@eq nat _ _) => lia : all.
#[global] Hint Extern 0 (_ = _) => progress f_equal : all.
#[global] Hint Unfold on_snd snd : all.

Lemma on_snd_eq_id_spec {A B} (f : B -> B) (x : A * B) :
  f (snd x) = snd x <->
  on_snd f x = x.
Proof.
  destruct x; simpl; unfold on_snd; simpl. split; congruence.
Qed.
#[global] Hint Resolve -> on_snd_eq_id_spec : all.
#[global] Hint Resolve -> on_snd_eq_spec : all.

Lemma map_def_gen_eq_spec {A B} (f f' : aname -> aname) (g g' h h' : A -> B) (x : def A) :
  f (dname x) = f' (dname x) ->
  g (dtype x) = g' (dtype x) ->
  h (dbody x) = h' (dbody x) ->
  map_def_gen f g h x = map_def_gen f' g' h' x.
Proof.
  intros. cbv[map_def_gen]; f_equal; auto.
Qed.
#[global] Hint Resolve map_def_gen_eq_spec : all.

Lemma map_def_eq_spec {A B} (f f' g g' : A -> B) (x : def A) :
  f (dtype x) = g (dtype x) ->
  f' (dbody x) = g' (dbody x) ->
  map_def f f' x = map_def g g' x.
Proof.
  now apply map_def_gen_eq_spec.
Qed.
#[global] Hint Resolve map_def_eq_spec : all.

Lemma map_def_id_spec {A} (f f' : A -> A) (x : def A) :
  f (dtype x) = (dtype x) ->
  f' (dbody x) = (dbody x) ->
  map_def f f' x = x.
Proof.
  intros. rewrite (map_def_eq_spec _ _ id id); auto. destruct x; auto.
Qed.
#[global] Hint Resolve map_def_id_spec : all.

Lemma tfix_gen_map_spec {A B} {P : aname -> Type} {P' P'' : A -> Type} {l}
                        {f f' : aname -> aname} {g g' h h' : A -> B} :
  tFixProp_gen P P' P'' l -> (forall x, P x -> f x = f' x) ->
  (forall x, P' x -> g x = g' x) -> (forall x, P'' x -> h x = h' x) ->
  map (map_def_gen f g h) l = map (map_def_gen f' g' h') l.
Proof.
  intros.
  eapply All_map_eq. red in X. eapply All_impl; eauto. simpl.
  intros. destruct X0, p;
  eapply map_def_gen_spec; eauto.
Qed.

Lemma tfix_map_spec {A B} {P P' : A -> Type} {l} {f f' g g' : A -> B} :
  tFixProp P P' l -> (forall x, P x -> f x = g x) ->
  (forall x, P' x -> f' x = g' x) ->
  map (map_def f f') l = map (map_def g g') l.
Proof.
  intros; eapply tfix_gen_map_spec; eauto.
  Qed.

Record judgment_ {universe Term} := Judge {
  j_term : option Term;
  j_typ : Term;
  j_univ : option universe;
  j_rel : option relevance;
}.
Arguments judgment_ : clear implicits.
Arguments Judge {universe Term} _ _ _.

Definition judgment_map {univ T A} (f: T -> A) (j : judgment_ univ T) :=
  Judge (option_map f (j_term j)) (f (j_typ j)) (j_univ j) (j_rel j).

Section Contexts.
  Context {term : Type}.
  (** *** The context of De Bruijn indices *)

  Record context_decl := mkdecl {
    decl_name : aname ;
    decl_body : option term ;
    decl_type : term
  }.
  Derive NoConfusion for context_decl.
End Contexts.

Arguments context_decl : clear implicits.

Notation Typ typ := (Judge None typ None None).
Notation TypRel typ rel := (Judge None typ None (Some rel)).
Notation TermTyp tm ty := (Judge (Some tm) ty None None).
Notation TermTypRel tm ty rel := (Judge (Some tm) ty None (Some rel)).
Notation TermoptTyp tm typ := (Judge tm typ None None).
Notation TermoptTypRel tm typ rel := (Judge tm typ None (Some rel)).
Notation TypUniv ty u := (Judge None ty (Some u) None).
Notation TypUnivRel ty u rel := (Judge None ty (Some u) (Some rel)).
Notation TermTypUniv tm ty u := (Judge (Some tm) ty (Some u) None).

Notation j_vass na ty := (TypRel ty na.(binder_relevance)).
Notation j_vass_s na ty s := (TypUnivRel ty s na.(binder_relevance)).
Notation j_vdef na b ty := (TermTypRel b ty na.(binder_relevance)).
Notation j_decl d := (TermoptTypRel (decl_body d) (decl_type d) (decl_name d).(binder_relevance)).
Notation j_decl_s d s := (Judge (decl_body d) (decl_type d) s (Some (decl_name d).(binder_relevance))).

Definition map_decl_gen {term term'} (fname : aname -> aname) (f : term -> term')
  (d : context_decl term) : context_decl term' :=
  {| decl_name := fname d.(decl_name);
    decl_body := option_map f d.(decl_body);
    decl_type := f d.(decl_type) |}.

Definition map_decl {term term'} := (@map_decl_gen term term' id).

Lemma map_decl_def {term term'} (f : term -> term') d :
  map_decl f d = map_decl_gen id f d.
Proof. reflexivity. Qed.

Lemma compose_map_decl_gen {term term' term''} (gname fname : aname -> aname)
  (g : term -> term') (f : term' -> term'') x :
  map_decl_gen fname f (map_decl_gen gname g x) = map_decl_gen (fname ∘ gname) (f ∘ g) x.
Proof.
  destruct x as [? [?|] ?]; reflexivity.
Qed.

Lemma compose_map_decl {term term' term''} (g : term -> term') (f : term' -> term'') x :
  map_decl f (map_decl g x) = map_decl (f ∘ g) x.
Proof.
  destruct x as [? [?|] ?]; reflexivity.
Qed.

Lemma map_decl_gen_ext {term term'} (fname gname : aname -> aname) (f g : term -> term') x :
  (forall n, fname n = gname n) -> (forall x, f x = g x) -> map_decl_gen fname f x = map_decl_gen gname g x.
Proof.
  intros H H'; destruct x as [? [?|] ?]; rewrite /map_decl_gen /=; f_equal.
  all: try now rewrite H.
  all: now rewrite H'.
Qed.

Lemma map_decl_ext {term term'} (f g : term -> term') x : (forall x, f x = g x) -> map_decl f x = map_decl g x.
Proof.
  now apply map_decl_gen_ext.
Qed.

#[global] Instance map_decl_proper {term term'} : Proper (`=1` ==> Logic.eq ==> Logic.eq) (@map_decl term term').
Proof.
  intros f g Hfg x y ->. now apply map_decl_ext.
Qed.

#[global] Instance map_decl_pointwise {term term'} : Proper (`=1` ==> `=1`) (@map_decl term term').
Proof. intros f g Hfg x. rewrite /map_decl.
  destruct x => /=. cbv[map_decl_gen]. cbn. f_equal.
  - now rewrite Hfg.
  - apply Hfg.
Qed.
(*

#[global] Instance pointwise_subrelation {A B} : subrelation (`=1`) (@Logic.eq A ==> @Logic.eq B)%signature.
Proof.
  intros f g Hfg x y ->. now rewrite Hfg.
Qed.

#[global] Instance pointwise_subrelation_inv {A B} : subrelation (@Logic.eq A ==> @Logic.eq B)%signature  (`=1`).
Proof.
  intros f g Hfg x. now specialize (Hfg x x eq_refl).
Qed.*)

Definition map_context_gen {term term'} (f : aname -> aname) (g : term -> term')
  (c : list (context_decl term)) :=
  List.map (map_decl_gen f g) c.

Notation map_context := (map_context_gen id).

#[global] Instance map_context_proper {term term'} : Proper (`=1` ==> Logic.eq ==> Logic.eq) (@map_context_gen term term' id).
Proof.
  intros f g Hfg l' l ->. induction l; auto.
  unfold map_context in *; cbn; f_equal; auto.
  cbv [map_decl_gen]; f_equal; now rewrite Hfg.
Qed.

Lemma map_context_gen_length {term term'} fname (f : term -> term') l :
  #|map_context_gen fname f l| = #|l|.
Proof. now unfold map_context_gen; rewrite length_map. Qed.

Lemma map_context_length {term term'} (f : term -> term') l : #|map_context f l| = #|l|.
Proof. now unfold map_context; rewrite length_map. Qed.
#[global] Hint Rewrite @map_context_gen_length @map_context_length : len.

Definition test_decl {term} (f : term -> bool) (d : context_decl term) : bool :=
  option_default f d.(decl_body) true && f d.(decl_type).

#[global] Instance test_decl_proper {term} : Proper (`=1` ==> Logic.eq ==> Logic.eq) (@test_decl term).
Proof.
  intros f g Hfg [na [b|] ty] ? <- => /=; rewrite /test_decl /=;
  now rewrite Hfg.
Qed.

Definition snoc {A} (Γ : list A) (d : A) := d :: Γ.

Notation " Γ ,, d " := (snoc Γ d) (at level 20, d at next level).

Definition app_context {A} (Γ Γ': list A) := Γ' ++ Γ.

Notation "Γ ,,, Γ'" := (app_context Γ Γ') (at level 25, Γ' at next level, left associativity).

Lemma app_context_nil_l {T} Γ : [] ,,, Γ = Γ :> list T.
Proof.
  unfold app_context. rewrite app_nil_r. reflexivity.
Qed.

Lemma app_context_assoc {T} Γ Γ' Γ'' : Γ ,,, (Γ' ,,, Γ'') = Γ ,,, Γ' ,,, Γ'' :> list T.
Proof. unfold app_context; now rewrite app_assoc. Qed.

Lemma app_context_cons {T} Γ Γ' A : Γ ,,, (Γ' ,, A) = (Γ ,,, Γ') ,, A :> list T.
Proof. exact (app_context_assoc _ _ [A]). Qed.

Lemma app_context_push {T} Γ Δ Δ' d : (Γ ,,, Δ ,,, Δ') ,, d = (Γ ,,, Δ ,,, (Δ' ,, d)) :> list T.
Proof using Type.
  reflexivity.
Qed.

Lemma snoc_app_context {T Γ Δ d} : (Γ ,,, (d :: Δ)) =  (Γ ,,, Δ) ,,, [d] :> list T.
Proof using Type.
  reflexivity.
Qed.

Lemma app_context_length {T} (Γ Γ' : list T) : #|Γ ,,, Γ'| = #|Γ'| + #|Γ|.
Proof. unfold app_context. now rewrite length_app. Qed.
#[global] Hint Rewrite @app_context_length : len.

Lemma nth_error_app_context_ge {T} v Γ Γ' :
  #|Γ'| <= v -> nth_error (Γ ,,, Γ') v = nth_error Γ (v - #|Γ'|) :> option T.
Proof. apply nth_error_app_ge. Qed.

Lemma nth_error_app_context_lt {T} v Γ Γ' :
  v < #|Γ'| -> nth_error (Γ ,,, Γ') v = nth_error Γ' v :> option T.
Proof. apply nth_error_app_lt. Qed.


Definition ondecl {A} (P : A -> Type) (d : context_decl A) :=
  option_default P d.(decl_body) unit × P d.(decl_type).

Notation onctx P := (All (ondecl P)).

Section ContextMap.
  Context {term term' : Type} (f : nat -> term -> term').

  Fixpoint mapi_context (c : list (context_decl term)) : list (context_decl term') :=
    match c with
    | d :: Γ => map_decl (f #|Γ|) d :: mapi_context Γ
    | [] => []
  end.
End ContextMap.

#[global] Instance mapi_context_proper {term term'} : Proper (`=2` ==> Logic.eq ==> Logic.eq) (@mapi_context term term').
Proof.
  intros f g Hfg Γ ? <-.
  induction Γ as [|[na [b|] ty] Γ]; simpl; auto; f_equal; auto; now rewrite Hfg.
Qed.

Lemma mapi_context_length {term} (f : nat -> term -> term) l : #|mapi_context f l| = #|l|.
Proof.
  induction l; simpl; auto.
Qed.
#[global] Hint Rewrite @mapi_context_length : len.

Section ContextTest.
  Context {term : Type} (f : term -> bool).

  Fixpoint test_context (c : list (context_decl term)) : bool :=
    match c with
    | d :: Γ => test_context Γ && test_decl f d
    | [] => true
    end.
End ContextTest.

#[global] Instance test_context_proper {term} : Proper (`=1` ==> Logic.eq ==> Logic.eq) (@test_context term).
Proof.
  intros f g Hfg Γ ? <-.
  induction Γ as [|[na [b|] ty] Γ]; simpl; auto; f_equal; auto; now rewrite Hfg.
Qed.

Section ContextTestK.
  Context {term : Type} (f : nat -> term -> bool) (k : nat).

  Fixpoint test_context_k (c : list (context_decl term)) : bool :=
    match c with
    | d :: Γ => test_context_k Γ && test_decl (f (#|Γ| + k)) d
    | [] => true
    end.
End ContextTestK.

#[global] Instance test_context_k_proper {term} : Proper (`=1` ==> Logic.eq ==> Logic.eq ==> Logic.eq) (@test_context_k term).
Proof.
  intros f g Hfg k ? <- Γ ? <-.
  induction Γ as [|[na [b|] ty] Γ]; simpl; auto; f_equal; auto; now rewrite Hfg.
Qed.

Section Contexts.
  Context {term term' term'' : Type}.
  Notation context term := (list (context_decl term)).

  Lemma test_decl_impl (f g : term -> bool) x : (forall x, f x -> g x) ->
    test_decl f x -> test_decl g x.
  Proof using Type.
    intros Hf; rewrite /test_decl.
    move/andb_and=> [Hd Hb].
    apply/andb_and; split; eauto.
    destruct (decl_body x); simpl in *; eauto.
  Qed.

  Definition onctx_k (P : nat -> term -> Type) k (ctx : context term) :=
    Alli (fun i d => ondecl (P (Nat.pred #|ctx| - i + k)) d) 0 ctx.

  Lemma ondeclP {P : term -> Type} {p : term -> bool} {d : context_decl term} :
    (forall x, reflectT (P x) (p x)) ->
    reflectT (ondecl P d) (test_decl p d).
  Proof using Type.
    intros hr.
    rewrite /ondecl /test_decl; destruct d as [decl_name decl_body decl_type]; cbn.
    destruct (hr decl_type) => //;
    destruct (reflect_option_default hr decl_body) => /= //; now constructor.
  Qed.

  Lemma onctxP {p : term -> bool} {ctx : context term} :
    reflectT (onctx p ctx) (test_context p ctx).
  Proof using Type.
    eapply equiv_reflectT.
    - induction 1; simpl; auto. rewrite IHX /= //.
      now move/(ondeclP reflectT_pred): p0.
    - induction ctx.
      * constructor.
      * move => /= /andb_and [Hctx Hd]; constructor; eauto.
        now move/(ondeclP reflectT_pred): Hd.
  Qed.

  Lemma map_decl_type (f : term -> term') decl : f (decl_type decl) = decl_type (map_decl f decl).
  Proof using Type. destruct decl; reflexivity. Qed.

  Lemma map_decl_body (f : term -> term') decl : option_map f (decl_body decl) = decl_body (map_decl f decl).
  Proof using Type. destruct decl; reflexivity. Qed.

  Lemma map_decl_gen_id : @map_decl_gen term term id id =1 id.
  Proof using Type. intros d; now destruct d as [? [] ?]. Qed.

  Lemma map_decl_id : @map_decl term term id =1 id.
  Proof using Type. intros d; now destruct d as [? [] ?]. Qed.

  Lemma option_map_decl_body_map_decl (f : term -> term') x :
    option_map decl_body (option_map (map_decl f) x) =
    option_map (option_map f) (option_map decl_body x).
  Proof using Type. destruct x; reflexivity. Qed.

  Lemma option_map_decl_type_map_decl (f : term -> term') x :
    option_map decl_type (option_map (map_decl f) x) =
    option_map f (option_map decl_type x).
  Proof using Type. destruct x; reflexivity. Qed.

  Definition fold_context_gen_k fname (f : nat -> term -> term') Γ :=
    List.rev (mapi (fun k' decl => map_decl_gen fname (f k') decl) (List.rev Γ)).

  Definition fold_context_k := (fold_context_gen_k id).

  Arguments fold_context_k f Γ%_list_scope.

  Lemma fold_context_gen_k_alt f fname Γ :
    fold_context_gen_k fname f Γ =
    mapi (fun k' d => map_decl_gen fname (f (Nat.pred (length Γ) - k')) d) Γ.
  Proof using Type.
    unfold fold_context_k, fold_context_gen_k. rewrite rev_mapi. rewrite List.rev_involutive.
    apply mapi_ext. intros. f_equal. now rewrite List.length_rev.
  Qed.

  Lemma fold_context_k_alt f Γ :
    fold_context_k f Γ =
    mapi (fun k' d => map_decl (f (Nat.pred (length Γ) - k')) d) Γ.
  Proof using Type. apply fold_context_gen_k_alt. Qed.

  Lemma mapi_context_fold f Γ :
    mapi_context f Γ = fold_context_k f Γ.
  Proof using Type.
    setoid_replace f with (fun k => f (k - 0)) using relation
      (pointwise_relation nat (pointwise_relation term (@Logic.eq term')))%signature at 1.
    rewrite fold_context_k_alt. unfold mapi.
    generalize 0.
    induction Γ as [|d Γ]; intros n; simpl; auto. f_equal.
    rewrite IHΓ. rewrite mapi_rec_Sk.
    apply mapi_rec_ext => k x. intros.
    apply map_decl_ext => t. lia_f_equal.
    intros k. now rewrite Nat.sub_0_r.
  Qed.

  Lemma fold_context_k_tip f d : fold_context_k f [d] = [map_decl (f 0) d].
  Proof using Type. reflexivity. Qed.

  Lemma fold_context_k_length f Γ : length (fold_context_k f Γ) = length Γ.
  Proof using Type.
    unfold fold_context_k. now rewrite !List.length_rev mapi_length List.length_rev.
  Qed.

  Lemma fold_context_gen_k_snoc0 fname f Γ d :
    fold_context_gen_k fname f (d :: Γ) =
      fold_context_gen_k fname f Γ ,, map_decl_gen fname (f (length Γ)) d.
  Proof using Type.
    unfold fold_context_gen_k.
    rewrite !rev_mapi !rev_involutive. unfold mapi; rewrite mapi_rec_eqn.
    unfold snoc. f_equal. now rewrite Nat.sub_0_r List.length_rev.
    rewrite mapi_rec_Sk. simpl. apply mapi_rec_ext. intros.
    rewrite length_app !List.length_rev. simpl. f_equal. f_equal. lia.
  Qed.

  Lemma fold_context_k_snoc0 f Γ d :
    fold_context_k f (d :: Γ) = fold_context_k f Γ ,, map_decl (f (length Γ)) d.
  Proof using Type.
    apply fold_context_gen_k_snoc0.
  Qed.

  Lemma fold_context_k_app f Γ Δ :
    fold_context_k f (Δ ++ Γ)
    = fold_context_k (fun k => f (length Γ + k)) Δ ++ fold_context_k f Γ.
  Proof using Type.
    unfold fold_context_k, fold_context_gen_k.
    rewrite List.rev_app_distr.
    rewrite mapi_app. rewrite <- List.rev_app_distr. f_equal. f_equal.
    apply mapi_ext. intros. f_equal. rewrite List.length_rev. f_equal.
  Qed.

  Local Set Keyed Unification.

  Equations mapi_context_In (ctx : context term) (f : nat -> forall (x : context_decl term), In x ctx -> context_decl term) : context term :=
  mapi_context_In nil _ := nil;
  mapi_context_In (cons x xs) f := cons (f #|xs| x _) (mapi_context_In xs (fun n x H => f n x _)).

  Lemma mapi_context_In_spec (f : nat -> term -> term) (ctx : context term) :
    mapi_context_In ctx (fun n (x : context_decl term) (_ : In x ctx) => map_decl (f n) x) =
    mapi_context f ctx.
  Proof using Type.
    remember (fun n (x : context_decl term) (_ : In x ctx) => map_decl (f n) x) as g.
    funelim (mapi_context_In ctx g) => //=; rewrite (H f0) ; trivial.
  Qed.

  Equations fold_context_In (ctx : context term) (f : context term -> forall (x : context_decl term), In x ctx -> context_decl term) : context term :=
  fold_context_In nil _ := nil;
  fold_context_In (cons x xs) f :=
    let xs' := fold_context_In xs (fun n x H => f n x _) in
    cons (f xs' x _) xs'.

  Equations fold_context (f : context term -> context_decl term -> context_decl term) (ctx : context term) : context term :=
    fold_context f nil := nil;
    fold_context f (cons x xs) :=
      let xs' := fold_context f xs in
      cons (f xs' x ) xs'.

  Lemma fold_context_length f Γ : #|fold_context f Γ| = #|Γ|.
  Proof using Type.
    now apply_funelim (fold_context f Γ); intros; simpl; auto; f_equal.
  Qed.


  Lemma fold_context_In_spec (f : context term -> context_decl term -> context_decl term) (ctx : context term) :
    fold_context_In ctx (fun n (x : context_decl term) (_ : In x ctx) => f n x) =
    fold_context f ctx.
  Proof using Type.
    remember (fun n (x : context_decl term) (_ : In x ctx) => f n x) as g.
    funelim (fold_context_In ctx g) => //=; rewrite (H f0); trivial.
  Qed.

  #[global]
  Instance fold_context_Proper : Proper (`=2` ==> `=1`) fold_context.
  Proof using Type.
    intros f f' Hff' x.
    funelim (fold_context f x); simpl; auto. simp fold_context.
    now rewrite (H f' Hff').
  Qed.

  (** This function allows to forget type annotations on a binding context.
  Useful to relate the "compact" case representation in terms, with
  its typing relation, where the context has types *)
  Definition forget_types (c : list (BasicAst.context_decl term)) : list aname :=
    map decl_name c.

End Contexts.
#[global] Hint Rewrite @fold_context_length @fold_context_k_length : len.

Section Contexts.
  Context {term term' term'' : Type}.
  Notation context term := (list (context_decl term)).

  Lemma fold_context_k_id (x : context term) : fold_context_k (fun i x => x) x = x.
  Proof using Type.
    rewrite fold_context_k_alt.
    rewrite /mapi. generalize 0.
    induction x; simpl; auto.
    intros n.
    f_equal; auto.
    now rewrite map_decl_id.
  Qed.

  Lemma fold_context_k_compose (f : nat -> term' -> term) (g : nat -> term'' -> term') Γ :
    fold_context_k f (fold_context_k g Γ) =
    fold_context_k (fun i => f i ∘ g i) Γ.
  Proof using Type.
    rewrite !fold_context_k_alt mapi_mapi.
    apply mapi_ext => i d.
    rewrite compose_map_decl. apply map_decl_ext => t.
    now len.
  Qed.

  Lemma fold_context_gen_k_ext (fname gname : aname -> aname) (f g : nat -> term' -> term) Γ :
    fname =1 gname ->
    f =2 g ->
    fold_context_gen_k fname f Γ = fold_context_gen_k gname g Γ.
  Proof using Type.
    intros hn hfg.
    induction Γ; simpl; auto; rewrite !fold_context_gen_k_snoc0.
    rewrite IHΓ. f_equal. apply map_decl_gen_ext; auto.
    intro. now apply hfg.
  Qed.

  Lemma fold_context_k_ext (f g : nat -> term' -> term) Γ :
    f =2 g ->
    fold_context_k f Γ = fold_context_k g Γ.
  Proof using Type.
    now apply fold_context_gen_k_ext.
  Qed.

  #[global] Instance fold_context_gen_k_proper : Proper (pointwise_relation _ Logic.eq ==> pointwise_relation nat (pointwise_relation _ Logic.eq) ==> Logic.eq ==> Logic.eq)
    (@fold_context_gen_k term' term).
  Proof using Type.
    intros fn gn Hfgn f g Hfg x y <-. now apply fold_context_gen_k_ext.
  Qed.

  #[global] Instance fold_context_k_proper : Proper (pointwise_relation nat (pointwise_relation _ Logic.eq) ==> Logic.eq ==> Logic.eq)
    (@fold_context_k term' term).
  Proof using Type.
    intros f g Hfg x y <-. now apply fold_context_k_ext.
  Qed.

  Lemma alli_fold_context_k_prop (f : nat -> context_decl term -> bool) (g : nat -> term' -> term) ctx :
    alli f 0 (fold_context_k g ctx) =
    alli (fun i x => f i (map_decl (g (Nat.pred #|ctx| - i)) x)) 0 ctx.
  Proof using Type.
    now rewrite fold_context_k_alt /mapi alli_mapi.
  Qed.

  Lemma test_decl_map_decl f g x : (@test_decl term) f (map_decl g x) = @test_decl term (f ∘ g) x.
  Proof using Type.
    rewrite /test_decl /map_decl /=.
    f_equal. rewrite /option_default.
    destruct (decl_body x) => //.
  Qed.

  Lemma map_fold_context_gen_k (fname gname : aname -> aname) (f : term' -> term)
    (g : nat -> term'' -> term') ctx :
    map (map_decl_gen fname f) (fold_context_gen_k gname g ctx) =
      fold_context_gen_k (fname ∘ gname) (fun i => f ∘ g i) ctx.
  Proof using Type.
    rewrite !fold_context_gen_k_alt map_mapi.
    apply mapi_ext => i d. now rewrite compose_map_decl_gen.
  Qed.

  Lemma map_fold_context_k (f : term' -> term) (g : nat -> term'' -> term') ctx :
    map (map_decl f) (fold_context_k g ctx) = fold_context_k (fun i => f ∘ g i) ctx.
  Proof using Type. apply map_fold_context_gen_k. Qed.

  Lemma map_context_mapi_context (f : term' -> term) (g : nat -> term'' -> term') (ctx : list (BasicAst.context_decl term'')) :
    map_context f (mapi_context g ctx) =
    mapi_context (fun i => f ∘ g i) ctx.
  Proof using Type.
    rewrite !mapi_context_fold. now unfold map_context; rewrite map_fold_context_k.
  Qed.

  Lemma mapi_context_map (f : nat -> term' -> term) (g : context_decl term'' -> context_decl term') ctx :
    mapi_context f (map g ctx) = mapi (fun i => map_decl (f (Nat.pred #|ctx| - i)) ∘ g) ctx.
  Proof using Type.
    rewrite mapi_context_fold fold_context_k_alt mapi_map. now len.
  Qed.

  Lemma map_context_map (f : term' -> term) (g : context_decl term'' -> context_decl term') ctx :
    map_context f (map g ctx) = map (map_decl f ∘ g) ctx.
  Proof using Type.
    induction ctx; simpl; f_equal; auto.
  Qed.

  Lemma map_map_context (f : context_decl term' -> term) (g : term'' -> term') ctx :
    map f (map_context g ctx) = map (f ∘ map_decl g) ctx.
  Proof using Type.
    now rewrite /map_context map_map_compose.
  Qed.

  Lemma fold_context_gen_k_map (fname gname : aname -> aname )(f : nat -> term' -> term)
    (g : term'' -> term') Γ :
    fold_context_gen_k fname f (map_context_gen gname g Γ) =
    fold_context_gen_k (fname ∘ gname) (fun k => f k ∘ g) Γ.
  Proof using Type.
    rewrite !fold_context_gen_k_alt mapi_map.
    apply mapi_ext => n d //. len.
    now rewrite compose_map_decl_gen.
  Qed.

  Lemma fold_context_k_map (f : nat -> term' -> term) (g : term'' -> term') Γ :
    fold_context_k f (map_context g Γ) =
    fold_context_k (fun k => f k ∘ g) Γ.
  Proof using Type. apply fold_context_gen_k_map. Qed.


  Lemma fold_context_k_map_comm (f : nat -> term -> term) (g : term -> term) Γ :
    (forall i x, f i (g x) = g (f i x)) ->
    fold_context_k f (map_context g Γ) = map_context g (fold_context_k f Γ).
  Proof using Type.
    intros Hfg.
    rewrite !fold_context_k_alt mapi_map.
    rewrite /map_context map_mapi.
    apply mapi_ext => i x.
    rewrite compose_map_decl_gen compose_map_decl.
    apply map_decl_ext => t.
    rewrite Hfg.
    now len.
  Qed.

  Lemma mapi_context_map_context (f : nat -> term' -> term) (g : term'' -> term') ctx :
    mapi_context f (map_context g ctx) =
    mapi_context (fun i => f i ∘ g) ctx.
  Proof using Type.
    now rewrite !mapi_context_fold fold_context_k_map.
  Qed.

  Lemma map_mapi_context (f : context_decl term' -> term) (g : nat -> term'' -> term') ctx :
    map f (mapi_context g ctx) = mapi (fun i => f ∘ map_decl (g (Nat.pred #|ctx| - i))) ctx.
  Proof using Type.
    now rewrite mapi_context_fold fold_context_k_alt map_mapi.
  Qed.

  Lemma map_context_id (ctx : context term) : map_context id ctx = ctx.
  Proof using Type.
    unfold map_context.
    now rewrite map_decl_gen_id map_id.
  Qed.

  Lemma forget_types_length (ctx : list (context_decl term)) :
    #|forget_types ctx| = #|ctx|.
  Proof using Type.
    now rewrite /forget_types length_map.
  Qed.

  Lemma map_decl_name_fold_context_k (f : nat -> term' -> term) ctx :
    map decl_name (fold_context_k f ctx) = map decl_name ctx.
  Proof using Type.
    now rewrite fold_context_k_alt map_mapi /= mapi_cst_map.
  Qed.

  Lemma forget_types_fold_context_k (f : nat -> term' -> term) ctx :
    forget_types (fold_context_k f ctx) = forget_types ctx.
  Proof using Type.
    now rewrite /forget_types map_decl_name_fold_context_k.
  Qed.

  Lemma All2_fold_impl_onctx (P : context term -> context term -> context_decl term -> context_decl term -> Type) P' Γ Δ Q :
    onctx Q Γ ->
    All2_fold P Γ Δ ->
    (forall Γ Δ d d',
      All2_fold P Γ Δ ->
      P Γ Δ d d' ->
      ondecl Q d ->
      P' Γ Δ d d') ->
    All2_fold P' Γ Δ.
  Proof using Type.
    intros onc cr Hcr.
    induction cr; depelim onc; constructor; intuition eauto.
  Qed.

  Lemma All2_fold_mapi (P : context term -> context term -> context_decl term -> context_decl term -> Type) (Γ Δ : context term) f g :
    All2_fold (fun Γ Δ d d' =>
      P (mapi_context f Γ) (mapi_context g Δ) (map_decl (f #|Γ|) d) (map_decl (g #|Γ|) d')) Γ Δ
    <~> All2_fold P (mapi_context f Γ) (mapi_context g Δ).
  Proof using Type.
    split.
    - induction 1; simpl; constructor; intuition auto;
      now rewrite <-(All2_fold_length X).
    - induction Γ as [|d Γ] in Δ |- *; destruct Δ as [|d' Δ]; simpl; intros H;
      depelim H; constructor; simpl in *; auto.
      pose proof (All2_fold_length H). len in H0.
      now rewrite <- H0 in p.
  Qed.

  Lemma All2_fold_map_gen {P : context term -> context term -> context_decl term -> context_decl term -> Type} {Γ Δ : context term} fname gname f g :
    All2_fold (fun Γ Δ d d' =>
                 P (map_context_gen fname f Γ) (map_context_gen gname g Δ)
                   (map_decl_gen fname f d) (map_decl_gen gname g d')) Γ Δ <~>
      All2_fold P (map_context_gen fname f Γ) (map_context_gen gname g Δ).
  Proof using Type.
    split.
    - induction 1; simpl; constructor; intuition auto;
        now rewrite <-(All2_fold_length X).
    - induction Γ as [|d Γ] in Δ |- *; destruct Δ as [|d' Δ]; simpl; intros H;
        depelim H; constructor; auto.
  Qed.

  Lemma All2_fold_map {P : context term -> context term -> context_decl term -> context_decl term -> Type} {Γ Δ : context term} f g :
    All2_fold (fun Γ Δ d d' =>
      P (map_context f Γ) (map_context g Δ) (map_decl f d) (map_decl g d')) Γ Δ <~>
    All2_fold P (map_context f Γ) (map_context g Δ).
  Proof using Type.
    apply All2_fold_map_gen.
  Qed.

  Lemma All2_fold_cst_map {P : context_decl term -> context_decl term -> Type} {Γ Δ : context term} {f g} :
    All2_fold (fun _ _ d d' => P (f d) (g d')) Γ Δ <~>
    All2_fold (fun _ _ => P) (map f Γ) (map g Δ).
  Proof using Type.
    split.
    - induction 1; simpl; constructor; intuition auto;
      now rewrite <-(All2_fold_length X).
    - induction Γ as [|d Γ] in Δ |- *; destruct Δ as [|d' Δ]; simpl; intros H;
        depelim H; constructor; auto.
  Qed.


End Contexts.

#[global] Hint Rewrite @map_mapi_context
  @map_fold_context_k @mapi_context_map @map_context_map @map_map_context
  @mapi_context_map_context @map_context_mapi_context : map.
#[global] Hint Rewrite @forget_types_length : len.

(** Primitive types models (axiom free) *)

(** Model of unsigned integers *)
Definition uint_size := 63.
Definition uint_wB := (2 ^ (Z.of_nat uint_size))%Z.
Definition uint63_model := { z : Z | ((0 <=? z) && (z <? uint_wB))%Z }.

Definition string_of_uint63_model (i : uint63_model) := string_of_Z (proj1_sig i).

(** Model of floats *)
Definition prec := 53%Z.
Definition emax := 1024%Z.
(** We consider valid binary encordings of floats as our model *)
Definition float64_model := sig (SpecFloat.valid_binary prec emax).
